
&НаКлиенте
Процедура Расш1_ЗагрузкаExcelПосле(Команда) 
	ЗагрузкаИзExcel = Истина;   
	
	Если НЕ ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите путь к файлу.";
		Сообщение.Поле = "ПутьКФайлу";
		Сообщение.Сообщить();	  
		
		Возврат;
	КонецЕсли;
	
	Файл = Новый ДвоичныеДанные(ПутьКФайлу);   //2 
	АдресВХ = ПоместитьВоВременноеХранилище(Файл, ЭтаФорма.УникальныйИдентификатор);  //Создаю ВХ
	ТЗ_СоставУпаковкиВсеСОтборами = ЗагрузкаФайлаНаСервере(АдресВХ); // Вернули таблицу на клиент    
	
	//Обойти таблицу снизу вверх
	сч = ТЗ_СоставУпаковкиВсеСОтборами.Количество() - 1;
	Пока сч >= 0 Цикл 
		стр = ТЗ_СоставУпаковкиВсеСОтборами[сч];   
		
		Если ЗначениеЗаполнено(Стр.КоличествоЗаявокЧислоМест) Тогда 
			// Добавляем состав упаковки со строкой количества заявок
			ДобавитьСтрокуСоставаУпаковки(Стр);
			
			// Запускаем формирование упаковки
			МаксимальноеКоличествоУпаковок = СоставУпаковки.Итог("КоличествоУпаковок");  
			КоличествоУпаковок = Стр.КоличествоЗаявокЧислоМест;
			
			СформироватьУпаковки(Неопределено);  
			СоставУпаковки.Очистить();    
		Иначе          
			// Добавляем состав упаковки
			ДобавитьСтрокуСоставаУпаковки(Стр);
		КонецЕсли;		
		
		сч = сч - 1;
	КонецЦикла;
КонецПроцедуры 

&НаКлиенте
Процедура ДобавитьСтрокуСоставаУпаковки(Стр)
	НоваяСтрока = СоставУпаковки.Добавить();	   
	НоваяСтрока.Номенклатура = Стр.Номенклатура;   
	НоваяСтрока.Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");    
	НоваяСтрока.Количество = Стр.Количество;
	НоваяСтрока.КоличествоУпаковок = Стр.КоличествоУпаковок;	
КонецПроцедуры

&НаСервере
Функция ЗагрузкаФайлаНаСервере(АдресВХ)
	Данные = ПолучитьИзВременногоХранилища(АдресВХ);   //3. получаю из ВХ двоичные данные
	ИмяВФ = ПолучитьИмяВременногоФайла("xlsx");   
	Данные.Записать(ИмяВФ);    
	
	ТаблицаЗначений =  ЗагрузитьМетодом_EXCEL1C(ИмяВФ, "Sheet1", 1,  3);   
	МассивАртикулов = Новый Массив();	           
	Для Каждого Стр Из ТаблицаЗначений Цикл   
		Если Стр.НомерСтроки = 1 Тогда         
			Продолжить; 	
		КонецЕсли;
		МассивАртикулов.Добавить(СокрЛП(Стр.N2));    
	КонецЦикла;
	
	ТаблицаСНоменклатурой = ВернутьТаблицуСНоменклатурой(МассивАртикулов);   
	
	ТЗ_СоставУпаковкиВсеСОтборами = Новый ТаблицаЗначений();
	ТЗ_СоставУпаковкиВсеСОтборами.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура")); 
	ТЗ_СоставУпаковкиВсеСОтборами.Колонки.Добавить("Количество");
	ТЗ_СоставУпаковкиВсеСОтборами.Колонки.Добавить("КоличествоУпаковок");   
	ТЗ_СоставУпаковкиВсеСОтборами.Колонки.Добавить("КоличествоЗаявокЧислоМест");
	
	тДерево = РеквизитФормыВЗначение("ИтоговоеДеревоУпаковок");
	тДерево.Строки.Очистить();
	ЗначениеВРеквизитФормы(тДерево, "ИтоговоеДеревоУпаковок");   
	
	Для Каждого Стр Из ТаблицаЗначений Цикл   
		Если Стр.НомерСтроки = 1 Тогда
			Продолжить;	
		КонецЕсли; 
		
 		Структура = Новый Структура();       
		Структура.Вставить("Артикул", Стр.N2);                           
		Структура.Вставить("Размер", Формат(Стр.N5, "ЧГ=0"));
		Структура.Вставить("Цвет", Стр.N3);  
		
		МассивОтбор = ТаблицаСНоменклатурой.НайтиСтроки(Структура);   // По сочетанию трех колонок я ищу ссылку на номенклатуру  
		
		Для Каждого Элм Из МассивОтбор Цикл
			ОтборОстатки = Новый Структура();
			ОтборОстатки.Вставить("Номенклатура", Элм.Ссылка);
			
			МассивОтборОстатки = ОстаткиПродукции.НайтиСтроки(ОтборОстатки);  
			Если МассивОтборОстатки.Количество() > 0 Тогда
           		НоваяСтрока = ТЗ_СоставУпаковкиВсеСОтборами.Добавить();
				НоваяСтрока.Номенклатура = Элм.Ссылка;
				НоваяСтрока.Количество = Стр.N7;
				НоваяСтрока.КоличествоУпаковок = Стр.N6;
				НоваяСтрока.КоличествоЗаявокЧислоМест = Стр.N4; 
			Иначе
				Если МассивОтбор.Количество() = 1 Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Не найдена номенлатура в таблице 'Список продукции' из файла загрузки excel: " + Элм.Ссылка.Наименование;
					Сообщение.Сообщить();		
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;  
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТЗ_СоставУпаковкиВсеСОтборами);
КонецФункции

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	//1.открытие файла при нажатии на кнопку ПутьКФайлу. Прописала значение кнопке путь к файлу.
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберете файл";
	Диалог.ПолноеИмяФайла = "";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Каталог = "";  
	Если Диалог.Выбрать() Тогда 
		ПутьКФайлу = Диалог.ПолноеИмяФайла;      //записываю путь к нашему файлу
	КонецЕсли;
КонецПроцедуры                          

&НаСервереБезКонтекста
Функция ВернутьТаблицуСНоменклатурой(МассивАртикулов)      //3.2
	лТекст = "
	|ВЫБРАТЬ
	|	НоменклатураДополнительныеРеквизиты.Ссылка КАК Ссылка,
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Артикул КАК Артикул,
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Код КАК Код,
	|	НоменклатураДополнительныеРеквизиты.Значение КАК Размер,
	|	NULL КАК Цвет
	|ПОМЕСТИТЬ ВТ_ДанныеПоНоменклатуре
	|ИЗ
	|	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|ГДЕ
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Артикул В(&МассивАртикулов)
	|	И НоменклатураДополнительныеРеквизиты.Свойство.Имя = &Размер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураДополнительныеРеквизиты.Ссылка,  
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Артикул КАК Артикул, 
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Код КАК Код,
	|	NULL,
	|	НоменклатураДополнительныеРеквизиты.Значение
	|ИЗ
	|	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|ГДЕ
	|	НоменклатураДополнительныеРеквизиты.Ссылка.Артикул В(&МассивАртикулов)
	|	И НоменклатураДополнительныеРеквизиты.Свойство.Имя = &Цвет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПоНоменклатуре.Ссылка КАК Ссылка, 
	|	ВТ_ДанныеПоНоменклатуре.Артикул КАК Артикул,
	|	ВТ_ДанныеПоНоменклатуре.Код КАК Код,
	|	МАКСИМУМ(ВТ_ДанныеПоНоменклатуре.Размер) КАК Размер,
	|	МАКСИМУМ(ВТ_ДанныеПоНоменклатуре.Цвет) КАК Цвет
	|ИЗ
	|	ВТ_ДанныеПоНоменклатуре КАК ВТ_ДанныеПоНоменклатуре
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеПоНоменклатуре.Ссылка,
	|	ВТ_ДанныеПоНоменклатуре.Артикул,
	|	ВТ_ДанныеПоНоменклатуре.Код
	|";
	
	лЗапрос = Новый Запрос(лТекст);
	Размер = "Размер_409de51ae644492c87a143df8c3c2264";      //обьявляем переменную размер в нее помещаем свойство.
	Цвет = "Цвет_710f8bcbca5b43dc8faa35a37c6f45d1";           //обьявляем переменную цвет в нее помещаем свойство.
	
	// Установка параметров.
	лЗапрос.УстановитьПараметр("МассивАртикулов", МассивАртикулов);
	лЗапрос.УстановитьПараметр("Размер", Размер);
	лЗапрос.УстановитьПараметр("Цвет", Цвет);
	
	ТаблицаЗначений = лЗапрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗначений;
КонецФункции      

&НаКлиенте
&Вместо("ДобавитьНовуюПроизвольнуюУпаковкуВИтоговоеДеревоУпаковок")
Функция Расш1_ДобавитьНовуюПроизвольнуюУпаковкуВИтоговоеДеревоУпаковок()
	НовыеСтроки = Новый Массив;
	ИсключаемыеПоля = "Количество,ХешСуммаУпаковки,СоставУпаковки,ВложенныеЕдиницы";
	ЭлементыДерева = ИтоговоеДеревоУпаковок.ПолучитьЭлементы();
	
	СтрокаАгрегации = ЭлементыДерева.Вставить(0);
	НовыеСтроки.Добавить(СтрокаАгрегации.ПолучитьИдентификатор());
	СтрокаАгрегации.Количество                = СоставУпаковки.Итог("КоличествоУпаковок");  
	СтрокаАгрегации.КоличествоИтог            = КоличествоУпаковок;
	СтрокаАгрегации.ПредставлениеУпаковки     = ПросклонятьЕдиницу(СтрокаАгрегации.Количество, ВидПродукции, Истина);
	СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьУпаковку(КоличествоУпаковок);
	
	СтрокаАгрегации.УпаковкаСформирована = Ложь;
	СтрокаАгрегации.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Истина;
	
	СодержимоеУпаковки = Новый Массив;
	
	Для Каждого СтрокаСоставаУпаковки Из СоставУпаковки Цикл
		
		Если СтрокаСоставаУпаковки.КоличествоУпаковок = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СоставАгрегации = СтрокаАгрегации.ПолучитьЭлементы();
		СтрокаПродукции = Неопределено;
		
		Для Каждого СтрокаСоставаАгрегации Из СоставАгрегации Цикл
			Если СтрокаСоставаАгрегации.Номенклатура = СтрокаСоставаУпаковки.Номенклатура
				И СтрокаСоставаАгрегации.Характеристика = СтрокаСоставаУпаковки.Характеристика
				И СтрокаСоставаАгрегации.Серия = СтрокаСоставаУпаковки.Серия
				И СтрокаСоставаАгрегации.GTIN = СтрокаСоставаУпаковки.GTIN Тогда
				СтрокаПродукции = СтрокаСоставаАгрегации;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаПродукции = Неопределено Тогда
			
			СтрокаПродукции = СоставАгрегации.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаПродукции, СтрокаСоставаУпаковки,, ИсключаемыеПоля);
			Если СтрокаПродукции.ЭтоНабор
				Или СтрокаПродукции.ЭтоГрупповаяУпаковка Тогда
				ЗаполнитьВложенныеЕдиницы(СтрокаПродукции, СтрокаСоставаУпаковки);
			КонецЕсли;
			
			СтрокаПродукции.УпаковкаСформирована = Ложь;
			СтрокаПродукции.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Ложь;
			
			ПредставлениеНоменклатуры = ИнтеграцияИСМПКлиентСервер.ПредставлениеGTINОстаткиПоВидуПродукции(
				СтрокаПродукции.ПредставлениеНоменклатуры, ВидПродукции);
			
			СтрокаПродукции.Содержимое = ПредставлениеНоменклатуры(
				СтрокаПродукции.Номенклатура, СтрокаПродукции.Характеристика, СтрокаПродукции.Серия,
				ПредставлениеНоменклатуры);
			
			Если ЗначениеЗаполнено(СтрокаСоставаУпаковки.Номенклатура) Тогда
				
				ЭлементСодержимого = Новый Структура("Номенклатура, Характеристика, Серия, Количество");
				ЭлементСодержимого.Номенклатура   = СтрокаСоставаУпаковки.Номенклатура;
				ЭлементСодержимого.Характеристика = СтрокаСоставаУпаковки.Характеристика;
				ЭлементСодержимого.Серия          = СтрокаСоставаУпаковки.Серия;
				ЭлементСодержимого.Количество     = СтрокаСоставаУпаковки.КоличествоУпаковок;
				
				СодержимоеУпаковки.Добавить(ЭлементСодержимого);
				
			ИначеЕсли ЗначениеЗаполнено(СтрокаПродукции.Содержимое) Тогда
				
				ЭлементСодержимого = Новый Структура("Номенклатура, Характеристика, Серия, Количество");
				ЭлементСодержимого.Номенклатура = СтрокаПродукции.Содержимое;
				ЭлементСодержимого.Количество   = СтрокаСоставаУпаковки.КоличествоУпаковок;
				
				СодержимоеУпаковки.Добавить(ЭлементСодержимого);
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаПродукции.Количество     = СтрокаПродукции.Количество + СтрокаСоставаУпаковки.КоличествоУпаковок; 
		//Чепикова Я.
		СтрокаПродукции.КоличествоИтог = СтрокаПродукции.КоличествоИтог + СтрокаСоставаУпаковки.КоличествоУпаковок * КоличествоУпаковок;
		//Чепикова Я.
		Если СтрокаПродукции.ЭтоНабор Тогда
			Если СтрокаСоставаУпаковки.ВложенныеЕдиницы.Количество() = 1 Тогда
				СтрокаПродукции.Коэффициент = СтрокаСоставаУпаковки.ВложенныеЕдиницы[0].Количество;
			КонецЕсли;
			СтрокаПродукции.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix");
			СтрокаПродукции.ПредставлениеУпаковки     = ПросклонятьНабор(СтрокаПродукции.Количество);
			СтрокаПродукции.ПредставлениеУпаковкиИтог = ПросклонятьНабор(СтрокаПродукции.КоличествоИтог);
		ИначеЕсли СтрокаПродукции.ЭтоГрупповаяУпаковка Тогда
			СтрокаПродукции.Коэффициент  = СтрокаСоставаУпаковки.ВложенныеЕдиницы[0].Количество;
			СтрокаПродукции.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix");
			СтрокаПродукции.ПредставлениеУпаковки     = ПросклонятьГрупповуюУпаковку(СтрокаПродукции.Количество, ВидПродукции);
			СтрокаПродукции.ПредставлениеУпаковкиИтог = ПросклонятьГрупповуюУпаковку(СтрокаПродукции.КоличествоИтог, ВидПродукции);
		Иначе
			СтрокаПродукции.Коэффициент               = 1;
			СтрокаПродукции.ПредставлениеУпаковки     = СтрокаСоставаУпаковки.ЕдиницаИзмерения;
			СтрокаПродукции.ПредставлениеУпаковкиИтог = ПросклонятьЕдиницу(СтрокаПродукции.КоличествоИтог, ВидПродукции);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СоставАгрегации.Количество() = 1 Тогда
		СтрокаАгрегации.Коэффициент = СоставАгрегации[0].Количество * СоставАгрегации[0].Коэффициент;
	Иначе
		СтрокаАгрегации.Коэффициент = 0;
	КонецЕсли;
	
	СтрокаАгрегации.Содержимое = ПредставлениеСоставаУпаковки(СодержимоеУпаковки);
	
	//Чепикова Я. 
	ЗагрузкаИзExcel = Ложь;  
	Элементы.КоличествоУпаковок.Доступность = Истина;
	//Чепикова Я.
	Возврат НовыеСтроки;
КонецФункции

//Инфостар	
Функция ЗагрузитьМетодом_EXCEL1C(Знач ФайлEXCEL, Знач ИмяЛиста = "", Знач СтрокаЗаголовка = 1, НачСтрока = 0, КонСтрока = 0, КолвоСтрокФайла = 0) Экспорт  // 3.1 взяла с инфостара и не меняла не одной строки
	Перем ТабличныйДокумент, ОбластьФайла, КолВоКолонокФайла, ИмяКолонки, Область, ТекущаяОбласть, нСтрока, нКолонка, НоваяСтрокаТФ, ЗначениеЯчейки;
	Перем ТаблицаРезультат;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Попытка
		// Выполняется долго на больших файлах.
		ТабличныйДокумент.Прочитать(ФайлEXCEL, СпособЧтенияЗначенийТабличногоДокумента.Значение);    // СпособЧтенияЗначенийТабличногоДокумента - новый параметр платформы 8.3.6. Второе значение "Текст".
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Внимание);
		Возврат Новый ТаблицаЗначений;
	КонецПопытки;
	
	ОбластьФайла = ТабличныйДокумент;		
	// Платформа 8.3.10 (Облать = Лист данных).
	КолВоСтрокФайла = ОбластьФайла.ПолучитьРазмерОбластиДанныхПоВертикали();
	КолВоКолонокФайла = ОбластьФайла.ПолучитьРазмерОбластиДанныхПоГоризонтали();
	
	// Проверка заполненности листа.
	Если КолвоСтрокФайла = 0 Тогда
		// Завершение работы.
		// Закрытие Объектов.
		ТабличныйДокумент = Неопределено;
		Возврат Новый ТаблицаЗначений;    // В случае ошибки возвращаем пустую таблицу значений.
	КонецЕсли;
	
	// Создание результирующей таблицы, в которую будут записываться считанные из файла данные.
	ТаблицаРезультат = Новый ТаблицаЗначений;
	
	// Формирование колонок результирующей таблицы.
	
	// "НомерСтроки" - для наглядности и удобства.
	// В зависимости от разрабатываемой обработки.
	// "Сопоставлено" - может быть другим.
	// Здесь же могут быть добавлены другие колонки, не формируемые из содержимого файла.
	ТаблицаРезультат.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"), "№", 4);
	ТаблицаРезультат.Колонки.Добавить("Сопоставлено", Новый ОписаниеТипов("Булево"), "Сопоставлено", 1);
	
	Для ит = 1 ПО КолВоКолонокФайла Цикл
		нКолонка = СтрЗаменить(ит, Символы.НПП, "");
		ИмяКолонки = "N" + нКолонка;
		ТаблицаРезультат.Колонки.Добавить(ИмяКолонки);
	КонецЦикла;
	
	// 1-я строка. Заголовки.
	НоваяСтрокаТФ = ТаблицаРезультат.Добавить();
	НоваяСтрокаТФ.НомерСтроки = 1;
	Для ит=1 ПО КолВоКолонокФайла Цикл
		нКолонка = СтрЗаменить(ит, Символы.НПП, "");
		ИмяКолонки = "N" + нКолонка;
		НоваяСтрокаТФ[ИмяКолонки] = ОбластьФайла.ПолучитьОбласть("R1" + "C"+нКолонка).ТекущаяОбласть.Текст;
		
		// Используется при формировании таблицы на форме обработки.
		ШиринаКолонки = ТаблицаРезультат.Колонки[ИмяКолонки].Ширина;
		ДлинаСтроки    = СтрДлина(СокрЛП(НоваяСтрокаТФ[ИмяКолонки]));
		ТаблицаРезультат.Колонки[ИмяКолонки].Ширина = ?(ШиринаКолонки < ДлинаСтроки, ДлинаСтроки, ШиринаКолонки);
	КонецЦикла;
	
	НачСтрока = ?(НачСтрока = 0, 2, НачСтрока);
	КонСтрока = ?(КонСтрока = 0, КолвоСтрокФайла, КонСтрока);
	
	Для нСтрокаТФ = НачСтрока ПО КонСтрока Цикл
		НоваяСтрокаТФ = ТаблицаРезультат.Добавить();
		НоваяСтрокаТФ[0] = нСтрокаТФ;
		нСтрока = СтрЗаменить(нСтрокаТФ, Символы.НПП, "");
		Для нКолонкаТФ = 1 ПО КолВоКолонокФайла Цикл
			нКолонка = СтрЗаменить(нКолонкаТФ, Символы.НПП, "");
			Область = ОбластьФайла.ПолучитьОбласть("R"+нСтрока+"C"+нКолонка);
			ТекущаяОбласть = Область.ТекущаяОбласть;
			Попытка
				ЗначениеЯчейки = ТекущаяОбласть.Значение;        // Число, Дата.
			Исключение
				ЗначениеЯчейки = СокрЛП(ТекущаяОбласть.Текст);    // Строка, Булево. (Булево как строка "ИСТИНА"/"ЛОЖЬ")
				Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
					ЗначениеЯчейки = ПреобразоватьПростоеЗначениеИзСтрокиВТипизованноеЗначение1С(ЗначениеЯчейки);
					Если ТипЗнч(ЗначениеЯчейки) = Тип("Строка") Тогда
						ЗначениеЯчейки = СокрЛП(ЗначениеЯчейки);
					КонецЕсли;
				Иначе
					ЗначениеЯчейки = Неопределено;
					Если Область.Рисунки.Количество() > 0 Тогда    // Изображение.
						ЗначениеЯчейки = ПолучитьЗначениеЯчейкиОбластиТабличногоДокументаСКартинками(Область, нСтрока, нКолонка, "УИД");
					КонецЕсли;
				КонецЕсли;
			КонецПопытки;
			
			ИмяКолонки = "N" + нКолонка;
			НоваяСтрокаТФ[ИмяКолонки] = ЗначениеЯчейки;
			
			// Используется при формировании таблицы на форме обработки.
			ШиринаКолонки = ТаблицаРезультат.Колонки[ИмяКолонки].Ширина;
			ДлинаСтроки    = СтрДлина(СокрЛП(НоваяСтрокаТФ[ИмяКолонки]));
			ТаблицаРезультат.Колонки[ИмяКолонки].Ширина = ?(ШиринаКолонки < ДлинаСтроки, ДлинаСтроки, ШиринаКолонки);
		КонецЦикла;
	КонецЦикла;
	
	// Юзабилити. Удалить пустые колонки.
	УдалитьКолонкиСНулевойШириной(ТаблицаРезультат);
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ПреобразоватьПростоеЗначениеИзСтрокиВТипизованноеЗначение1С(Знач ИсходноеЗначение) Экспорт
	
	Если НЕ ИсходноеЗначение = "" Тогда
		Если ТолькоЦифрыИЗапятаяВСтроке(ИсходноеЗначение, Истина, Ложь) Тогда
			Попытка
				Возврат ИсходноеЗначение;
			Исключение
				Возврат ИсходноеЗначение
			КонецПопытки;
		Иначе
			Если ВРег(ИсходноеЗначение) = "ИСТИНА" ИЛИ ВРег(ИсходноеЗначение) = ("ИСТИНА"+Символы.ПС) ИЛИ ВРег(ИсходноеЗначение) = "TRUE" ИЛИ ВРег(ИсходноеЗначение) = ("TRUE"+Символы.ПС) Тогда
				Возврат Истина;
			ИначеЕсли ВРег(ИсходноеЗначение) = "ЛОЖЬ" ИЛИ  ВРег(ИсходноеЗначение) = ("ЛОЖЬ"+Символы.ПС) ИЛИ ВРег(ИсходноеЗначение) = "FALSE" ИЛИ ВРег(ИсходноеЗначение) = ("FALSE"+Символы.ПС) Тогда
				Возврат Ложь;
			Иначе
				Возврат ПреобразоватьИзСтрокиВДату(ИсходноеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИсходноеЗначение
	
КонецФункции

Функция ПолучитьЗначениеЯчейкиОбластиТабличногоДокументаСКартинками(Знач Область, Знач нСтрока, Знач нКолонка, Знач ПравилоИмяФайлаКартинки = "УИД") Экспорт
	Перем Рисунок, ит, ИмяФайлаРисунка;
	Перем ЗначениеЯчейки;
	
	ит = 0;
	ЗначениеЯчейки = "";
	Для Каждого Рисунок ИЗ Область.Рисунки Цикл
		ит = ит + 1;
		Если ПравилоИмяФайлаКартинки = "УИД" Тогда
			ИмяФайлаРисунка = КаталогВременныхФайлов() + Новый УникальныйИдентификатор() + ".jpg";
		Иначе
			ИмяФайлаРисунка = КаталогВременныхФайлов() + "С" + нСтрока + "К" + нКолонка + ".jpg";
		КонецЕсли;
		Попытка
			Рисунок.Картинка.Записать(ИмяФайлаРисунка);
			ЗначениеЯчейки = ЗначениеЯчейки + ИмяФайлаРисунка + ?(ит < Область.Рисунки.Количество(), Символы.ПС, "");
		Исключение
			// Поле картинки недоступно для чтения.
		КонецПопытки;
	КонецЦикла;
	
	Возврат ЗначениеЯчейки;
	
КонецФункции

Функция ПреобразоватьИзСтрокиВДату(Знач СтрокаДаты) Экспорт
	Перем ScrptCtrl, OutDate;
	
	Попытка
		ScrptCtrl = Новый COMОбъект("MSScriptControl.ScriptControl");
		ScrptCtrl.Language="vbscript";
		OutDate = ScrptCtrl.Eval("CDate(""" + СтрокаДаты + """)");
		Возврат OutDate;
	Исключение
		//Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат СтрокаДаты;
	
КонецФункции

Функция ТолькоЦифрыИЗапятаяВСтроке(Знач СтрокаПроверки, Знач УчитыватьЛидирующиеНули = Истина, Знач УчитыватьПробелы = Истина) Экспорт
	
	Если ТипЗнч(СтрокаПроверки) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ УчитыватьПробелы Тогда
		СтрокаПроверки = СтрЗаменить(СтрокаПроверки, " ", "");
	КонецЕсли;
	
	Если Сред(СтрокаПроверки, 1, 1) = "-" Тогда
		СтрокаПроверки = Сред(СтрокаПроверки, 2, СтрДлина(СтрокаПроверки));
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаПроверки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ УчитыватьЛидирующиеНули Тогда
		Позиция = 1;
		// Взятие символа за границей строки возвращает пустую строку
		Пока Сред(СтрокаПроверки, Позиция, 1) = "0" Цикл
			Позиция = Позиция + 1;
		КонецЦикла;
		СтрокаПроверки = Сред(СтрокаПроверки, Позиция);
	КонецЕсли;
	
	// Если содержит только цифры, то в результате замен должна быть получена пустая строка
	// Проверять при помощи ПустаяСтрока нельзя, так как в исходной строке могут быть пробельные символы
	Возврат СтрДлина(
	СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
	СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
	СтрокаПроверки, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9", ""), ",", "")
	) = 0;
	
КонецФункции

Процедура УдалитьКолонкиСНулевойШириной(ТаблицаРезультат) Экспорт
	Перем МассивПустыхКолонок;
	
	// Найдем пустые колонки.
	МассивПустыхКолонок = Новый Массив;
	Для Каждого Колонка ИЗ ТаблицаРезультат.Колонки Цикл
		Если Колонка.Ширина = 0 Тогда
			МассивПустыхКолонок.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	// Удалим пустые колонки.
	Для Каждого ПустаяКолонка ИЗ МассивПустыхКолонок Цикл
		ТаблицаРезультат.Колонки.Удалить(ПустаяКолонка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
&Вместо("ДобавитьНовыеМонотоварныеУпаковкиВИтоговоеДеревоУпаковок")
Функция Расш1_ДобавитьНовыеМонотоварныеУпаковкиВИтоговоеДеревоУпаковок()
	Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		НаименованиеСодержимогоГрупповойУпаковки = НСтр("ru = 'блок'");
	Иначе
		НаименованиеСодержимогоГрупповойУпаковки = НСтр("ru = 'групповая упаковка'");
	КонецЕсли;
	
	НовыеСтроки = Новый Массив;
	ДобавленныеГрупповыеЭлементы = Новый Массив;
	ИсключаемыеПоля = "Количество,ПредставлениеУпаковки,ХешСуммаУпаковки,СоставУпаковки,ВложенныеЕдиницы";
	ЭлементыДерева = ИтоговоеДеревоУпаковок.ПолучитьЭлементы();
	ВГраницаСоставаУпаковки = СоставУпаковки.Количество() - 1;
	
	Для ИндексСтрокиСоставаУпаковки = 0 По ВГраницаСоставаУпаковки Цикл
		
		СтрокаСоставаУпаковки = СоставУпаковки[ВГраницаСоставаУпаковки - ИндексСтрокиСоставаУпаковки];
		
		Если СтрокаСоставаУпаковки.КоличествоУпаковок = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоВложенныхЕдиниц = 0;
		Если СтрокаСоставаУпаковки.ЭтоГрупповаяУпаковка Тогда
			КоличествоВложенныхЕдиниц = СтрокаСоставаУпаковки.ВложенныеЕдиницы[0].Количество;
		КонецЕсли;
		
		КоличествоСоставляющихНабора = 1;
		Если СтрокаСоставаУпаковки.ЭтоНабор Тогда
			КоличествоСоставляющихНабора = СтрокаСоставаУпаковки.ВложенныеЕдиницы.Количество();
		КонецЕсли;
		
		ПредставлениеНоменклатуры = ПредставлениеНоменклатуры(
			СтрокаСоставаУпаковки.Номенклатура,
			СтрокаСоставаУпаковки.Характеристика,
			СтрокаСоставаУпаковки.Серия,
			ИнтеграцияИСМПКлиентСервер.ПредставлениеGTINОстаткиПоВидуПродукции(
			СтрокаСоставаУпаковки.ПредставлениеНоменклатуры, ВидПродукции));
		
		ПредыдущаяСтрокаАгрегации = Неопределено;
		Если ЗначениеЗаполнено(СтрокаСоставаУпаковки.ХешСуммаУпаковки) Тогда
			
			МассивЭлементов = Новый Массив;
			ЗаполнитьМассивЭлементовЭлементаДерева(МассивЭлементов, СтрокаСоставаУпаковки.СоставУпаковки);
			ВГраница = МассивЭлементов.ВГраница();
			Для Индекс = 0 По ВГраница Цикл
				ЭлементСоставаУпаковки = МассивЭлементов[ВГраница - Индекс];
				Если КоличествоВложенныхЕдиниц > 0
					И ЭлементСоставаУпаковки.Коэффициент = КоличествоВложенныхЕдиниц Тогда
					Продолжить;
				КонецЕсли;
				
				Если Индекс = 0 Тогда
					СтрокаАгрегации = ЭлементыДерева.Вставить(0);
					СкопироватьЭлементыДерева(СтрокаАгрегации.СоставУпаковки, СтрокаСоставаУпаковки.СоставУпаковки);
					НовыеСтроки.Добавить(СтрокаАгрегации.ПолучитьИдентификатор());
					СтрокаАгрегации.Содержимое = СтрШаблон("%1, %2",
						ЭлементСоставаУпаковки.Наименование,
						ПредставлениеНоменклатуры);
					ДобавленныеГрупповыеЭлементы.Добавить(СтрокаАгрегации);
				Иначе
					СтрокаАгрегации = СтрокаАгрегации.ПолучитьЭлементы().Добавить();
					СтрокаАгрегации.Содержимое = ЭлементСоставаУпаковки.Наименование;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаАгрегации, СтрокаСоставаУпаковки,, ИсключаемыеПоля);
				
				СтрокаАгрегации.ХешСуммаУпаковки = ЭлементСоставаУпаковки.ХешСуммаУпаковки;
				СтрокаАгрегации.Упаковка         = ЭлементСоставаУпаковки.Упаковка;
				
				Если КоличествоСоставляющихНабора = 1 Тогда
					СтрокаАгрегации.Коэффициент = ЭлементСоставаУпаковки.Коэффициент;
				Иначе
					СтрокаАгрегации.Коэффициент = 0;
				КонецЕсли;
				
				СтрокаАгрегации.УпаковкаСформирована = Ложь;
				СтрокаАгрегации.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Истина;
				
				Если ЭлементСоставаУпаковки.КоличествоУпаковок = 0 Тогда 
					//++ Чепикова Я. 
					//было СтрокаАгрегации.Количество            = ЭлементСоставаУпаковки.Коэффициент;    
					СтрокаАгрегации.Количество            = ЭлементСоставаУпаковки.Коэффициент / СтрокаСоставаУпаковки.КоличествоУпаковок; 
					//-- Чепикова Я.
					СтрокаАгрегации.ПредставлениеУпаковки = СтрокаСоставаУпаковки.ЕдиницаИзмерения;
				Иначе
					СтрокаАгрегации.Количество            = ЭлементСоставаУпаковки.КоличествоУпаковок;
					СтрокаАгрегации.ПредставлениеУпаковки = МассивЭлементов[ВГраница - Индекс - 1].Наименование;
				КонецЕсли;
				Если Индекс = 0 Тогда
					СтрокаАгрегации.КоличествоИтог = СтрокаСоставаУпаковки.КоличествоУпаковок;
				Иначе
					СтрокаАгрегации.КоличествоИтог =
						ПредыдущаяСтрокаАгрегации.Количество * ПредыдущаяСтрокаАгрегации.КоличествоИтог;
				КонецЕсли;
				СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьУпаковку(СтрокаАгрегации.КоличествоИтог);
				ПредыдущаяСтрокаАгрегации = СтрокаАгрегации;
			КонецЦикла;
			
			Если СтрокаСоставаУпаковки.ЭтоНабор Тогда
				СтрокаАгрегации = СтрокаАгрегации.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаАгрегации, СтрокаСоставаУпаковки,, ИсключаемыеПоля);
				ЗаполнитьВложенныеЕдиницы(СтрокаАгрегации, СтрокаСоставаУпаковки);
				СтрокаАгрегации.Содержимое = НСтр("ru = 'набор'");
				СтрокаАгрегации.УпаковкаСформирована = Ложь;
				СтрокаАгрегации.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Ложь;
				СтрокаАгрегации.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix");
				СтрокаАгрегации.Количество = СтрокаАгрегации.ВложенныеЕдиницы.Итог("Количество");
				Если КоличествоСоставляющихНабора = 1 Тогда
					СтрокаАгрегации.Коэффициент = СтрокаСоставаУпаковки.ВложенныеЕдиницы[0].Количество;
				КонецЕсли;
				СтрокаАгрегации.ПредставлениеУпаковки = ПросклонятьЕдиницу(СтрокаАгрегации.Количество, ВидПродукции);
				СтрокаАгрегации.КоличествоИтог = СтрокаСоставаУпаковки.Количество;
				СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьНабор(СтрокаАгрегации.КоличествоИтог);
			ИначеЕсли СтрокаСоставаУпаковки.ЭтоГрупповаяУпаковка
				И КоличествоВложенныхЕдиниц > 0 Тогда
				СтрокаАгрегации = СтрокаАгрегации.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаАгрегации, СтрокаСоставаУпаковки,, ИсключаемыеПоля);
				ЗаполнитьВложенныеЕдиницы(СтрокаАгрегации, СтрокаСоставаУпаковки);
				СтрокаАгрегации.Содержимое = НаименованиеСодержимогоГрупповойУпаковки;
				СтрокаАгрегации.УпаковкаСформирована = Ложь;
				СтрокаАгрегации.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Ложь;
				СтрокаАгрегации.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix");
				СтрокаАгрегации.Количество = КоличествоВложенныхЕдиниц;
				СтрокаАгрегации.Коэффициент = КоличествоВложенныхЕдиниц;
				СтрокаАгрегации.ПредставлениеУпаковки = ПросклонятьЕдиницу(СтрокаАгрегации.Количество, ВидПродукции);
				СтрокаАгрегации.КоличествоИтог = Цел(СтрокаСоставаУпаковки.Количество / КоличествоВложенныхЕдиниц);
				СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьГрупповуюУпаковку(СтрокаАгрегации.КоличествоИтог, Видпродукции);
			КонецЕсли;
			
		Иначе
			
			СтрокаАгрегации = ЭлементыДерева.Вставить(0);
			НовыеСтроки.Добавить(СтрокаАгрегации.ПолучитьИдентификатор());
			ЗаполнитьЗначенияСвойств(СтрокаАгрегации, СтрокаСоставаУпаковки,, ИсключаемыеПоля);
			ЗаполнитьВложенныеЕдиницы(СтрокаАгрегации, СтрокаСоставаУпаковки);
			СтрокаАгрегации.УпаковкаСформирована = Ложь;
			СтрокаАгрегации.ТребуетсяУказаниеПараметровГенерацииШтрихкодов = Ложь;
			СтрокаАгрегации.ТипШтрихкода = ПредопределенноеЗначение("Перечисление.ТипыШтрихкодов.GS1_DataMatrix");
			Если СтрокаСоставаУпаковки.ЭтоНабор Тогда
				СтрокаАгрегации.Содержимое = СтрШаблон("%1, %2",
					НСтр("ru = 'набор'"),
					ПредставлениеНоменклатуры);
				СтрокаАгрегации.Количество = СтрокаАгрегации.ВложенныеЕдиницы.Итог("Количество");
				Если КоличествоСоставляющихНабора = 1 Тогда
					СтрокаАгрегации.Коэффициент = СтрокаСоставаУпаковки.ВложенныеЕдиницы[0].Количество;
				КонецЕсли;
				СтрокаАгрегации.ПредставлениеУпаковки = ПросклонятьЕдиницу(СтрокаАгрегации.Количество, ВидПродукции);
				СтрокаАгрегации.КоличествоИтог = СтрокаСоставаУпаковки.Количество;
				СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьНабор(СтрокаАгрегации.КоличествоИтог);
			ИначеЕсли СтрокаСоставаУпаковки.ЭтоГрупповаяУпаковка
				И КоличествоВложенныхЕдиниц > 0 Тогда
				СтрокаАгрегации.Содержимое = СтрШаблон("%1, %2",
					НаименованиеСодержимогоГрупповойУпаковки,
					ПредставлениеНоменклатуры);
				СтрокаАгрегации.Количество = КоличествоВложенныхЕдиниц;
				СтрокаАгрегации.Коэффициент = КоличествоВложенныхЕдиниц;
				СтрокаАгрегации.ПредставлениеУпаковки = ПросклонятьЕдиницу(СтрокаАгрегации.Количество, ВидПродукции);
				СтрокаАгрегации.КоличествоИтог = Цел(СтрокаСоставаУпаковки.Количество / КоличествоВложенныхЕдиниц);
				СтрокаАгрегации.ПредставлениеУпаковкиИтог = ПросклонятьГрупповуюУпаковку(СтрокаАгрегации.КоличествоИтог, Видпродукции);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Элементы.ГруппаСписокУпаковок.Видимость
		И Элементы.ИтоговоеДеревоУпаковок.Видимость Тогда
		Для Каждого ДобавленныйЭлемент Из ДобавленныеГрупповыеЭлементы Цикл
			Элементы.ИтоговоеДеревоУпаковок.Развернуть(ДобавленныйЭлемент.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
	КонецЕсли;
	
	Возврат НовыеСтроки;
КонецФункции



